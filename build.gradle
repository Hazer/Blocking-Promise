group 'io.vithor'
version '1.0.0'

buildscript {
    ext.kotlin_version = '1.1.2'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}
plugins {
    id "com.jfrog.bintray" version "1.7.3"
}

apply plugin: 'kotlin'
apply plugin: 'maven-publish'

sourceCompatibility = 1.6

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.+'
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
}

bintray {
    user = '<bintray user name>'
    key = '<bintray API key>'
    publications = ['mavenJava']

    pkg {
        repo = '<repository name on bintray>'
        name = rootProject.name

        version {
            name = project.version
            released = new Date()
            vcsTag = "v${project.version}"
        }
    }
}

task sourcesJar(type: Jar, dependsOn: project.classes) {
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: project.javadoc) {
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar, javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId project.bintray.pkg.name
            from components.java

            artifact sourcesJar {
                classifier = 'sources'
            }
            artifact javadocJar {
                classifier = 'javadoc'
            }
        }
    }
}
